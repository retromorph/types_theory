I := \x. x;
Y := \f. (\x. f (x x)) \x. f (x x);

TRUE := \x.\y. x;
FALSE := \x.\y. y;
& := \p.\q. p q FALSE;
| := \p.\q. p TRUE q;
~ := \p. p FALSE TRUE;
IF := \p.\a.\b. p a b;

+ := \x.\y.\s.\z. x s (y s z);
- := \m.\n. n(\n.\f.\x. n(\g.\h. h (g f))(\u. x)(\u. u)) m;
* := \x.\y.\s. x (y s);
ISZERO := \n. n (\x. FALSE) TRUE;
<= := \m.\n. ISZERO (- m n);
>= := \m.\n. ISZERO (- n m);
== := \m.\n. (& (<= m n) (<= n m));
< := \m.\n. & (<= m n) (~ (== m n));
> := \m.\n. & (>= m n) (~ (== m n));
/ := Y (
\div. \m. \n.
    IF (< m n)
    0
    (+ 1 (div (- m n) n))
);
FACTORIAL := Y (\f.\x. IF (== x 0) 1 (* x (f (- x 1))));

[] := \x.\y. (\f. f x y);
L := \p. p (\x.\y. x);
R := \p. p (\x.\y. y);
SWAP := \p. p (\x.\y. \f. f y x);

NIL := FALSE;
SINGLE := \h.\c.\n. c h n;
LIST := \h.\t.\c.\n. c h (t c n);
++ := \l.\t.\c.\n. l c (t c n);
ISNIL := \l. l (\h.\r. FALSE) TRUE;
HEAD := \l. l (\h.\r. h) FALSE;
FOLDR := LIST;
FOLDL := \f.\z.\l.
    (l (\h.\g.\x. g (f x h)) (\x. x)) z;
LENGTH := \l.
    l (\h.\r. (+ r 1)) 0;
FILTER := \p.\l.\c.\n.
    l (\h.\r. IF (p h) (c h r) r) n;
TAIL := \l.\c.\n. l (\h.\r.\g. g h (r c)) (\c. n) (\h.\t. t);
NTH := (Y (
\nth.\i.\arr.\n.
    IF (== i n)
    (HEAD arr)
    (nth (+ i 1) (TAIL arr) n)
) 0);

QSORT := Y (
\qsort.\arr.
    IF (<= (LENGTH arr) 1)
    (arr)
    (
        (\mid.
            (\smaller.\equal.\larger.
                ++ (++ (qsort smaller) equal) (qsort larger)
            )
            (FILTER (\x. (< x mid)) arr)
            (FILTER (\x. (== x mid)) arr)
            (FILTER (\x. (> x mid)) arr)
        ) (NTH arr (/ (LENGTH arr) 2))
    )
);

ARR := LIST 3 (LIST 2 (SINGLE 1));
ARRR := LIST 1 (LIST 2 (SINGLE 3));
ARRRR := LIST 6 (LIST 5 (LIST 4 (LIST 3 (LIST 2 (SINGLE 1)))));

LENGTH ARRRR
