I := \x. x;
Y := \f. (\x. f (x x)) \x. f (x x);

TRUE := \x.\y. x;
FALSE := \x.\y. y;
& := \p.\q. p q FALSE;
| := \p.\q. p TRUE q;
~ := \p. p FALSE TRUE;
IF := \p.\a.\b. p a b;

+ := \x.\y.\s.\z. x s (y s z);
- := \m.\n. n(\n.\f.\x. n(\g.\h. h (g f))(\u. x)(\u. u)) m;
* := \x.\y.\s. x (y s);
ISZERO := \n. n (\x. FALSE) TRUE;
<= := \m.\n. ISZERO (- m n);
>= := \m.\n. ISZERO (- n m);
== := \m.\n. (& (<= m n) (<= n m));
< := \m.\n. & (<= m n) (~ (== m n));
> := \m.\n. & (>= m n) (~ (== m n));
/ := Y (
\div. \m. \n.
    IF (< m n)
    0
    (+ 1 (div (- m n) n))
);

[] := \x.\y. (\f. f x y);
L := \p. p (\x.\y. x);
R := \p. p (\x.\y. y);
SWAP := \p. p (\x.\y. \f. f y x);

NIL := [] TRUE FALSE;
ISNIL := L;
LIST := \h.\t. [] FALSE ([] h t);
HEAD := \z. L (R z);
TAIL := \z. R (R z);
LENGTH := Y (
\f.\x.
    IF (ISNIL x)
    0
    (+ 1 (f (TAIL x)))
);
NTH := (Y (
\slice.\i.\arr.\n.
    IF (== i n)
    (HEAD arr)
    (slice (+ i 1) (TAIL arr) n)
) 0);
REVERSE := Y (
\reverse.\new_arr.\arr.
    IF (ISNIL arr)
    (new_arr)
    (reverse (LIST (HEAD arr) new_arr) (TAIL arr))
) NIL;
SLICE := (Y (
\slice.\i.\new_arr.\arr.\l.\r.
    IF (== i r)
    (REVERSE new_arr)
    (
        IF (< i l)
        (slice (+ i 1) new_arr (TAIL arr) l r)
        (slice (+ i 1) (LIST (HEAD arr) new_arr) (TAIL arr) l r)
    )
) 0 NIL);
FILTER := Y (
\filter.\new_arr.\condition.\arr.
    IF (ISNIL arr)
    (REVERSE new_arr)
    (
        IF (condition (HEAD arr))
        (filter (LIST (HEAD arr) new_arr) condition (TAIL arr))
        (filter new_arr condition (TAIL arr))
    )
) NIL;
++ := Y (
\concat.\new_arr.\larr.\rarr.
    IF (ISNIL larr)
        (IF (ISNIL rarr)
            (REVERSE new_arr)
            (concat (LIST (HEAD rarr) new_arr) larr (TAIL rarr))
        )
        (concat (LIST (HEAD larr) new_arr) (TAIL larr) rarr)
) NIL;
QSORT := Y (
\qsort.\arr.
    IF (<= (LENGTH arr) 1)
    (arr)
    (
        (\mid.
            (\smaller.\equal.\larger.
                ++ (++ (qsort smaller) equal) (qsort larger)
            )
            (FILTER (\x. (< x mid)) arr)
            (FILTER (\x. (== x mid)) arr)
            (FILTER (\x. (> x mid)) arr)
        ) (NTH arr (/ (LENGTH arr) 2))
    )
);
APPEND :=
(Y
  (\f.\xs.\ys.
    (ISNIL xs)
      ys
      (LIST (HEAD xs) (f (TAIL xs) ys))
  )
);


FACTORIAL := Y (\f.\x. IF (== x 0) 1 (* x (f (- x 1))));

TMP := LIST 3 (LIST 2 (LIST 1 NIL));
TTMP := LIST 2 (LIST 1 NIL);
TTTMP := LIST 2 NIL;
ARR := REVERSE(LIST 1 (LIST 2 (LIST 3 (LIST 4 (LIST 5 (LIST 6 NIL))))));

SORTED_ARR := QSORT TTMP;

/ 10 2